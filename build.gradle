buildscript { scriptHandler ->
    ext.keys = [
            'aws': [
                    'accessKeyId': project.hasProperty("awsMavenAccessKey") ? project.property("awsMavenAccessKey") : "${System.env.AWS_ACCES_KEY_ID}",
                    'secretKey': project.hasProperty("awsMavenSecretKey") ? project.property("awsMavenSecretKey") : "${System.env.AWS_SECRET_KEY}",
            ],
            'bintray': [
                    'user': project.hasProperty('bintrayUser') ? project.getProperty('bintrayUser') : '',
                    'apiKey': project.hasProperty('bintrayApiKey') ? project.getProperty('bintrayApiKey') : ''
            ]
    ]
    ext.versions = [
            'minSdk': 16,
            'compileSdk': 26,
            'buildTools': '26.0.2',

            'supportLibrary': '26.0.2',
            'androidPlugin': '3.0.0',
            'forsuredb': '0.12.13-test+1514211003905',
            'release': [
                    'buildTime': Long.toString(new Date().getTime()),
                    'remote': 'origin',
                    'skipCiCommitSuffix': '[skip ci]'
            ]
    ]
    ext.deps = [
            android: [
                    'gradlePlugin': "com.android.tools.build:gradle:${versions.androidPlugin}",
            ],
            'support': [
                    'compat': "com.android.support:support-compat:${versions.supportLibrary}",
                    'annotations': "com.android.support:support-annotations:${versions.supportLibrary}",
                    'appcompatv7': "com.android.support:appcompat-v7:${versions.supportLibrary}",
                    'recyclerviewv7': "com.android.support:recyclerview-v7:${versions.supportLibrary}",
                    'test': [
                            'runner': 'com.android.support.test:runner:1.0.1',
                    ],
            ],
            'bintrayPlugin': 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3',
            'fsryanGradle': [
                    'autosemver': 'com.fsryan.gradle.autosemver:autosemver-git:0.1.0-alpha',
                    'androidJavaCoverageMerger': 'com.fsryan.gradle.coverage:android-java-coverage-merger:0.0.0',
                    'findbugsVariants': 'com.fsryan.gradle.findbugsvariants:findbugs-variants:0.1.0-alpha'
            ],
            'androidMavenPlugin': 'com.github.dcendents:android-maven-gradle-plugin:1.5',
            'mavenS3Wagon': 'org.kuali.maven.wagons:maven-s3-wagon:1.2.1',
            guava: 'com.google.guava:guava:20.0',   // <-- do not include in artifacts--just test
            junit: 'junit:junit:4.12',
            mockito: 'org.mockito:mockito-core:1.10.19',
            forsuredb: [
                    'plugin': 'com.fsryan.gradle.forsuredb:forsuredbplugin:0.5.0',
                    'api': "com.fsryan.forsuredb:forsuredbapi:${versions.forsuredb}",
                    'compiler': "com.fsryan.forsuredb:forsuredbcompiler:${versions.forsuredb}",
                    'sqlitelib': "com.fsryan.forsuredb:sqlitelib:${versions.forsuredb}",
                    'dbinfo': "com.fsryan.forsuredb:dbinfo:${versions.forsuredb}",
                    'modelsGson': "com.fsryan.forsuredb:forsuredbmodels-gson:${versions.forsuredb}"
            ]
    ]

    repositories {
        maven {
            url  "http://dl.bintray.com/ryansgot/maven"
        }
        mavenLocal()
        maven {
            url 's3://repo.fsryan.com/release'
            credentials(AwsCredentials) {
                accessKey = keys.aws.accessKeyId
                secretKey = keys.aws.secretKey
            }
        }
        maven {
            url 's3://repo.fsryan.com/snapshot'
            credentials(AwsCredentials) {
                accessKey = keys.aws.accessKeyId
                secretKey = keys.aws.secretKey
            }
        }
        jcenter()
        google()
    }
    dependencies {
        classpath deps.android.gradlePlugin
        classpath deps.forsuredb.plugin
        classpath deps.bintrayPlugin
        classpath deps.androidMavenPlugin
        classpath deps.fsryanGradle.autosemver
        classpath deps.fsryanGradle.androidJavaCoverageMerger
        classpath deps.fsryanGradle.findbugsVariants
    }
}

long buildTime = new Date().getTime()

allprojects {
    apply plugin: 'autosemver-git'

    repositories {
        maven {
            url  "http://dl.bintray.com/ryansgot/maven"
        }
        mavenLocal()
        maven {
            url 's3://repo.fsryan.com/release'
            credentials(AwsCredentials) {
                accessKey = keys.aws.accessKeyId
                secretKey = keys.aws.secretKey
            }
        }
        maven {
            url 's3://repo.fsryan.com/snapshot'
            credentials(AwsCredentials) {
                accessKey = keys.aws.accessKeyId
                secretKey = keys.aws.secretKey
            }
        }
        jcenter()
        google()
    }

    autosemver {
        branchConfigs {
            integration {
                skipCiCommitMessageSuffix = versions.release.skipCiCommitSuffix
                pullRemote = versions.release.remote
                pushRemote = versions.release.remote
                versionIncrement = "patch"
                preRelease = 'test'
                metaData = versions.release.buildTime
            }
            alpha { // <-- for new features
                skipCiCommitMessageSuffix = versions.release.skipCiCommitSuffix
                pullRemote = versions.release.remote
                pushRemote = versions.release.remote
                versionIncrement = "minor"
                preRelease = 'alpha'
            }
            beta { // <-- for bug fixes
                skipCiCommitMessageSuffix = versions.release.skipCiCommitSuffix
                pullRemote = versions.release.remote
                pushRemote = versions.release.remote
                preRelease = 'beta'
            }
            master {    // <-- stable version
                skipCiCommitMessageSuffix = versions.release.skipCiCommitSuffix
                pullRemote = versions.release.remote
                pushRemote = versions.release.remote
            }
        }
    }
}