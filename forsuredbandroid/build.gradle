apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'jacoco'
apply plugin: 'android-java-coverage-merger'
apply plugin: 'findbugs'
apply plugin: 'findbugs-variants'

group = 'com.fsryan.forsuredb'

jacoco {
    toolVersion = "0.7.9"
}

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.compileSdk

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled = true
        }
    }

    flavorDimensions 'queryable'
    productFlavors {
        contentProvider {
            dimension 'queryable'
        }
        directDB {
            dimension 'queryable'
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true    // <-- Uri.parse() now returns null
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation deps.support.recyclerviewv7  // TODO: take this out (0.14.0)?

    api deps.forsuredb.api
    api deps.forsuredb.sqlitelib
    api deps.forsuredb.dbinfo

    testImplementation deps.test.junit
    testImplementation deps.test.guava
    testImplementation deps.test.mockito

    androidTestImplementation deps.forsuredb.modelsGson
    androidTestImplementation deps.test.android.runner
    androidTestImplementation deps.test.android.mockito
    androidTestImplementation deps.support.annotations
    androidTestImplementation deps.test.guava
    androidTestImplementation deps.test.junit
}

mergedReportConfig {
    classFilters {
        // buildType + flavor matches are additive rather than overriding
        debug {
            includes = ['**/com/fsryan/forsuredb/**']
            excludes = [
                    '**/R$*.class',                                 // generated R subclasses
                    '**/R.class',                                   // generated R classes
                    '**/*Test.class',                               // filter test classes
                    '**/BuildConfig*',                              // generated BuildConfig classes
            ]
        }
    }
}

apply from: 'publish.gradle'